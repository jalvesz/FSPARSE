#:include "common.fypp"
#:set KINDS_TYPES = REAL_KINDS_TYPES
!>---------------------------------------------------
!> Copyright 2023-present Transvalor S.A. (JosÃ© R. Alves Z.)
!>
!> Use of this source code is governed by a MIT
!> license that can be found in the LICENSE.md file
!>---------------------------------------------------
module test_fsparse
    use testdrive, only: new_unittest, unittest_type, error_type, check
    use fsparse
    use fsparse_constants
    implicit none
    
    contains

    !> Collect all exported unit tests
    subroutine collect_suite(testsuite)
        !> Collection of tests
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
            new_unittest('coo', test_coo), &
            new_unittest('coo2ordered', test_coo2ordered), &
            new_unittest('csr', test_csr), &
            new_unittest('csc', test_csc), &
            new_unittest('ell', test_ell),  &
            new_unittest('sellc', test_sellc),  &
            new_unittest('symmetries', test_symmetries), &
            new_unittest('cells2sparse', test_cells2sparse) &
        ]
    end subroutine

    subroutine test_coo(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            type(COO_${s1}$) :: COO
            ${t1}$, allocatable :: dense(:,:)
            ${t1}$, allocatable :: vec_x(:)
            ${t1}$, allocatable :: vec_y1(:), vec_y2(:)

            allocate( dense(4,5) , source = &
                    reshape(real([9,4, 0,4, &
                                  0,7, 8,0, &
                                  0,0,-1,5, &
                                  0,0, 8,6, &
                                 -3,0, 0,0],kind=wp),[4,5]) )
            
            call dense2coo( dense , COO )
            
            allocate( vec_x(5)  , source = 1._wp )
            allocate( vec_y1(4) , source = 0._wp )
            allocate( vec_y2(4) , source = 0._wp )

            vec_y1 = matmul( dense, vec_x )
            
            call check(error, all(vec_y1 == real([6,11,15,15],kind=wp)) )
            if (allocated(error)) return

            call matvec( COO, vec_x, vec_y2 )
            call check(error, all(vec_y1 == vec_y2) )
            if (allocated(error)) return
        end block
        #:endfor
    end subroutine

    subroutine test_coo2ordered(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        type(COO_sp) :: COO

        call COO%malloc(4,4,12)
        COO%data(:) = 1
        COO%index(:,1) = [1,2]
        COO%index(:,2) = [1,3]
        COO%index(:,3) = [1,4]
        COO%index(:,4) = [2,3]
        COO%index(:,5) = [2,4]
        COO%index(:,6) = [3,4]

        COO%index(:,7) = [2,3]
        COO%index(:,8) = [2,4]
        COO%index(:,9) = [2,5]
        COO%index(:,10) = [3,4]
        COO%index(:,11) = [3,5]
        COO%index(:,12) = [4,5]

        call coo2ordered(COO,sort_data=.true.)
        call check(error, COO%NNZ < 12 .and. COO%NNZ == 9 )
        if (allocated(error)) return

        call check(error, all(COO%data==[1,1,1,2,2,1,2,1,1])  )
        if (allocated(error)) return

        call check(error, all(COO%index(1,:)==[1,1,1,2,2,2,3,3,4])  )
        if (allocated(error)) return

        call check(error, all(COO%index(2,:)==[2,3,4,3,4,5,4,5,5])  )
        if (allocated(error)) return

    end subroutine 

    subroutine test_csr(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            type(CSR_${s1}$) :: CSR
            ${t1}$, allocatable :: vec_x(:)
            ${t1}$, allocatable :: vec_y(:)

            call CSR%malloc(4,5,10)
            CSR%data(:)   = real([9,-3,4,7,8,-1,8,4,5,6],kind=wp)
            CSR%col(:)    = [1,5,1,2,2,3,4,1,3,4]
            CSR%rowptr(:) = [1,3,5,8,11]
            
            allocate( vec_x(5) , source = 1._wp )
            allocate( vec_y(4) , source = 0._wp )
            call matvec( CSR, vec_x, vec_y )
            
            call check(error, all(vec_y == real([6,11,15,15],kind=wp)) )
            if (allocated(error)) return
        end block
        #:endfor
    end subroutine

    subroutine test_csc(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            type(CSC_${s1}$) :: CSC
            ${t1}$, allocatable :: vec_x(:)
            ${t1}$, allocatable :: vec_y(:)

            call CSC%malloc(4,5,10)
            CSC%data(:)   = real([9,4,4,7,8,-1,5,8,6,-3],kind=wp)
            CSC%row(:)    = [1,2,4,2,3,3,4,3,4,1]
            CSC%colptr(:) = [1,4,6,8,10,11]
            
            allocate( vec_x(5) , source = 1._wp )
            allocate( vec_y(4) , source = 0._wp )
            call matvec( CSC, vec_x, vec_y )
            
            call check(error, all(vec_y == real([6,11,15,15],kind=wp)) )
            if (allocated(error)) return
        end block
        #:endfor
    end subroutine

    subroutine test_ell(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            type(ELL_${s1}$) :: ELL
            ${t1}$, allocatable :: vec_x(:)
            ${t1}$, allocatable :: vec_y(:)

            call ELL%malloc(4,5,10)
            ELL%data(1,1:3)   = real([9,-3,0],kind=wp)
            ELL%data(2,1:3)   = real([4,7,0],kind=wp)
            ELL%data(3,1:3)   = real([8,-1,8],kind=wp)
            ELL%data(4,1:3)   = real([4,5,6],kind=wp)

            ELL%index(1,1:3) = [1,5,0]
            ELL%index(2,1:3) = [1,2,0]
            ELL%index(3,1:3) = [2,3,4]
            ELL%index(4,1:3) = [1,3,4]
            
            allocate( vec_x(5) , source = 1._wp )
            allocate( vec_y(4) , source = 0._wp )
            call matvec( ELL, vec_x, vec_y )
            
            call check(error, all(vec_y == real([6,11,15,15],kind=wp)) )
            if (allocated(error)) return
        end block
        #:endfor
        
    end subroutine

    subroutine test_sellc(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            type(SELLC_${s1}$) :: SELLC
            type(CSR_${s1}$)   :: CSR
            ${t1}$, allocatable :: vec_x(:)
            ${t1}$, allocatable :: vec_y(:)
            integer :: i

            call CSR%malloc(6,6,17)
            !           1   2   3   4   5   6 
            CSR%col = [ 1,      3,  4,         &
                            2,  3,      5,  6, &
                        1,  2,  3,             &
                                        5,  6, &
                                    4,  5,     &
                            2,          5,  6]
            CSR%rowptr = [1,4,8,11,13,15,18]
            CSR%data = [(real(i,kind=wp),i=1,CSR%nnz)] 
            
            call csr2sellc(CSR,SELLC,4)

            allocate( vec_x(6) , source = 1._wp )
            allocate( vec_y(6) , source = 0._wp )

            call matvec( SELLC, vec_x, vec_y )
            
            call check(error, all(vec_y == real([6,22,27,23,27,48],kind=wp)) )
            if (allocated(error)) return
        end block
        #:endfor
    end subroutine

    subroutine test_symmetries(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        #:for k1, t1, s1 in (KINDS_TYPES)
        block
            integer, parameter :: wp = ${k1}$
            type(COO_${s1}$) :: COO
            type(CSR_${s1}$) :: CSR
            ${t1}$, allocatable :: dense(:,:)
            ${t1}$, allocatable :: vec_x(:)
            ${t1}$, allocatable :: vec_y1(:), vec_y2(:), vec_y3(:)

            allocate( vec_x(4)  , source = 1._wp )
            allocate( vec_y1(4) , source = 0._wp )
            allocate( vec_y2(4) , source = 0._wp )
            allocate( vec_y3(4) , source = 0._wp )

            allocate( dense(4,4) , source = &
                    reshape(real([1,0,0,0, &
                                  2,1,0,0, &
                                  0,2,1,0,&
                                  0,0,2,1],kind=wp),[4,4]) )

            call dense2coo( dense , COO )
            COO%sym = k_SYMTRISUP
            call coo2csr(COO, CSR)

            dense(2,1) = 2._wp; dense(3,2) = 2._wp; dense(4,3) = 2._wp
            vec_y1 = matmul( dense, vec_x )
            call check(error, all(vec_y1 == [3,5,5,3]) )
            if (allocated(error)) return

            call matvec( COO , vec_x, vec_y2 )
            call check(error, all(vec_y1 == vec_y2) )
            if (allocated(error)) return

            call matvec( CSR , vec_x, vec_y3 )
            call check(error, all(vec_y1 == vec_y3) )
            if (allocated(error)) return
        end block
        #:endfor
    end subroutine

    subroutine test_cells2sparse(error)
        !> Error handling
        type(error_type), allocatable, intent(out) :: error
        type(COO_dp) :: COO, COO_n
        type(CSR_dp) :: CSR, CSR_n1
        real(8), allocatable :: dense(:,:), dense_n(:,:)
        integer :: i, k, dof
        integer, allocatable :: cells(:,:), cells_n(:,:)

        allocate(cells(4,2))
        cells(1:4,1) = [2,5,3,4]
        cells(1:4,2) = [4,1,3,2]
        call coo_from_cells(COO,cells,num_points=5,num_cells=2,selfloop=.true.,symtype=k_SYMTRIINF)
        call coo2csr(COO,CSR)

        call check(error, size(COO%data) == 14 .and. size( COO%index , dim=2 ) == 14 )
        if (allocated(error)) return

        dof = 4
        allocate(cells_n(4*dof,2))
        do i =1 , 4
            do k = 1, dof
            cells_n(dof*(i-1)+k,1) = dof*(cells(i,1)-1)+k
            cells_n(dof*(i-1)+k,2) = dof*(cells(i,2)-1)+k
            end do
        end do

        call coo_from_cells(COO_n,cells_n,num_points=5*dof,num_cells=2,selfloop=.true.,symtype=k_SYMTRIINF)
        call coo2csr(COO_n,CSR_n1)

        call csr_block_expansion(CSR,dof)

        call check(error, all(CSR%rowptr==CSR_n1%rowptr) .and. all(CSR%col==CSR_n1%col) )
        if (allocated(error)) return

    end subroutine

end module test_fsparse